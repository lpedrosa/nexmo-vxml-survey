apply plugin: 'java'
apply plugin: 'eclipse'

apply plugin: 'application'

sourceCompatibility = '1.8'
mainClassName = "com.lpedrosa.SurveyAppApplication"

run {
    if(project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

configurations {
    capsule
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.0'

    testCompile 'junit:junit:4.11'

    // capsule stuff
    capsule 'co.paralleluniverse:capsule:0.4.0'
}

task fullCapsule(type: Jar, dependsOn: jar) {
    archiveName = "nexmo-vxml-survey-app.jar"

    from jar
    from { configurations.runtime }

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' }

    manifest {
        attributes(
            'Main-Class': 'Capsule',
            'Application-Class': mainClassName,
            'Min-Java-Version': '1.8.0',
            'JVM-Args': run.jvmArgs.join(' '),
            'System-Properties': run.systemProperties.collect { k,v -> "$k=$v" }.join(' ')
        )
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
